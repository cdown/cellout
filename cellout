#!/usr/bin/awk -f

# Copyright (c) Christopher Down 2011-2012
# Copyright (c) Dave Reisner 2012
# See the COPYING file for copyright information.

function display_graphical(pct) {
    printf "["
    for (i = 0; i < pct / 25; i++)
        printf "#"
    while (i++ < 4)
        printf "-"
    printf "] "
}

BEGIN {
    if (ARGC == 2) {
        graphical = 0
        battery = ARGV[1]
    } else if ((ARGC == 3) && (ARGV[1] == "-g")) {
        graphical = 1
        battery = ARGV[2]
    } else {
        print "usage: cellout [-g] battery"
        exit 1
    }

    f_full = "/sys/class/power_supply/" battery "/charge_full"
    if ((getline charge_full < f_full) < 0) {
        f_full = "/sys/class/power_supply/" battery "/energy_full"
        if ((getline charge_full < f_full) < 0) {
            print "error: failed to read full charge data"
            exit 2
        }
    }

    f_now = "/sys/class/power_supply/" battery "/charge_now"
    if ((getline charge_now < f_now) < 0) {
        f_now = "/sys/class/power_supply/" battery "/energy_now"
        if ((getline charge_now < f_now) < 0) {
            print "error: failed to read current charge data"
            exit 2
        }
    }

    f_status = "/sys/class/power_supply/" battery "/status"
    if ((getline status < f_status) < 0) {
        print "error: failed to read battery status"
        exit 2
    }

    percent_full = charge_now / charge_full * 100
    if (percent_full > 100) {
        print "above 100"
        percent_full = 100
    }
    if (graphical == 1)
        display_graphical(percent_full)
    printf "%d%c\n", percent_full, status
}
