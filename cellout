#!/usr/bin/gawk -f

function display_text(now, full, status) {
    printf "%d%c\n", now / full * 100, status
}

function display_graphical(now, full, status) {
    percent = (now / full * 100)

    printf "["
    for (i = 0; i < percent / 25; i++)
        printf "#"
    while (i++ < 4)
        printf "-"
    printf "] "
}

BEGIN {
    if (length(ARGV) < 2) {
        print "usage: cellout [-g] battery"
        exit 1
    }

    if (ARGV[1] == "-g") {
        graphical = 1
        battery = ARGV[2]
    } else {
        graphical = 0
        battery = ARGV[1]
    }

    f_full = "/sys/class/power_supply/" battery "/energy_full"
    if ((getline charge_full < f_full) < 0) {
        printf "error: failed to read %s\n", f_full
        exit 2
    }

    f_now = "/sys/class/power_supply/" battery "/energy_now"
    if ((getline charge_now < f_now) < 0) {
        printf "error: failed to read %s\n", f_now
        exit 2
    }

    f_status = "/sys/class/power_supply/" battery "/status"
    if ((getline status < f_status) < 0) {
        printf "error: failed to read %s\n", f_status
        exit 2
    }

    if (graphical == 1)
        display_graphical(charge_now, charge_full, status)

    display_text(charge_now, charge_full, status)
}
